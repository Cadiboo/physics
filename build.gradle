buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.wynprice.cursemaven:CurseMaven:1.2.3'
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "com.wynprice.cursemaven"
apply plugin: 'com.github.johnrengelman.shadow'

version = mod_version
group = "gliby.minecraft.physics"
archivesBaseName = "glibysphysics"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

// todo look into versioning
minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replace "@VERSION@", project.version
    replace "@MC_VERSION@", version
}

dependencies {
    compileOnly curse.resolve("dynamic-lights", "2563244")
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.0'
    compile files('lib/gdx-bullet.jar')
    compile files('lib/gdx-bullet-natives.jar')
    compile files('lib/gdx.jar')
}

shadowJar {
    classifier = ''
    relocate 'org.apache.commons.math3', "${project.group}.shadow.org.apache.commons.math3"
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


jar {
    manifest {
        attributes 'FMLAT' : 'GlibysPhysics_at.cfg'
    }
}


List<String> tasks = gradle.startParameter.getTaskNames();
// IMPORTANT: If you set up with setupDecompWorkspace, you will not be able to use ./gradlew build or runClient.
// .gradle dir will need to be deleted to build.
if (tasks.contains("setupDecompWorkspace") || tasks.contains("genIntellijRuns")) {

    project.logger.lifecycle('setupDecompWorkspace or genIntellijRuns detected, applying IDEA workaround.')

    sourceSets {
        main { output.resourcesDir = output.classesDir }
    }

    idea { module { inheritOutputDirs = true } }
}
